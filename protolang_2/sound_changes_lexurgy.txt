Feature voicing(unvoiced, voiced)
Feature place(bilabial, dental, alveolar, velar, palatal, glottal, labialvelar)
Feature manner(nasal, plosive, fricative, trill, approximent)

Feature +lateral
Feature +aspirated

Feature height(close, mid, open)
Feature backness(front, central, back)
Feature roundedness(unrounded,rounded)
Feature type(consonant,vowel)
Feature (syllable) +stress
Feature (syllable) +tmpstress

Diacritic ˈ (before) (floating) [+stress]
Diacritic ⍼ (before) (floating) [+tmpstress]

Symbol m [voiced bilabial nasal consonant]
Symbol n [voiced alveolar nasal consonant]
Symbol p [unvoiced bilabial plosive consonant]
Symbol t [unvoiced alveolar plosive consonant]
Symbol k [unvoiced velar plosive consonant]
Symbol pʰ [+aspirated unvoiced bilabial plosive consonant]
Symbol tʰ [+aspirated unvoiced alveolar plosive consonant]
Symbol kʰ [+aspirated unvoiced velar plosive consonant]
Symbol s [unvoiced alveolar fricative consonant]
Symbol h [unvoiced glottal fricative consonant]
Symbol r [voiced alveolar trill consonant]
Symbol l [voiced alveolar +lateral approximent consonant]
Symbol j [voiced palatal approximent consonant]
Symbol w [voiced labialvelar approximent consonant]

Symbol a [open front unrounded vowel]
Symbol e [mid front unrounded vowel]
Symbol i [close front unrounded vowel]
Symbol o [mid back rounded vowel]
Symbol u [close back rounded vowel]
Symbol ɒ [open back rounded vowel]

#Symbol ai, au, ei, eu, ia, io, iu, oa, ɒa, oi, ou, ua, ui

Class consonant {m, n, p, t, k, pʰ, tʰ, kʰ, s, h, r, l, j, w}
Class monophthong {a,e,i,o,u,ɒ}
Class diphthong {ai, au, ei, eu, ia, io, iu, oa, ɒa, oi, ou, ua, ui}
Class vowel {@diphthong, @monophthong}
deromanizer:
    ph=>pʰ
    th=>tʰ
    kh=>kʰ
    '=>*
    ö=>ɒ
Syllables:
    @consonant? @vowel [nasal]?
assign-stress:
    [+stress] => [-stress +tmpstress]
    Then: (
        [+tmpstress] => [+stress -tmpstress]
        Else: <syl> => [+stress] / _ <syl> $
        Else: <syl> => [+stress] / _ $
    )
Syllables:
    explicit
u-gliding:
    u=>w/_ @vowel
    u=>w/@vowel _
i-gliding:
    i=>j/_ @vowel
    i=>j/@vowel _
oa=>ɒa
romanizer:
    ph=>p'h
    th=>t'h
    kh=>k'h
    pʰ=>ph
    tʰ=>th
    kʰ=>kh
    ɒ=>ö